beads 1 program Game2048

// -------------------------------------------
const
	gap = 4
	colors = [#cdc1b4 #eee4da #ede0c8 #f2b179 #f59563 #f67c5f #f65e3b #edcf72 #edcc61 #edc850 #edc53f #edc22e #edc22e]
	
	STORE_APP = "appdata"
	MYDB <=== { db_name:"com.beads.game2048", db_ver:1,	db_stores:[{store_name:STORE_APP} ]}
	
record game  
	won : yesno
	noMoreMoves : yesno
	moves
	score
	best

// -------------------------------------------
var
	board : array of num
	g : game
	blanks
	moved : yesno
	bkg : color
	color : color

// -------------------------------------------
calc addTile
	var
		v = 2 if random() < 0.9 else 4
		p = random_int(1,blanks)
	dec blanks
	loop reps:16 count:ix
		if is_numeric(board[ix])
			continue
		dec p
		if p == 0
			board[ix]=v
			exit

// -------------------------------------------
calc canMove
	var p = 1
	g.noMoreMoves = N
	if blanks > 0
		return
	loop reps:4
		loop reps:3
			if board[p] == board[p+1] or board[p] == board[p+4] 
				return
			inc p
		inc p	
	g.noMoreMoves = Y

// -------------------------------------------
calc execMove(
	dir
	)
	if g.won
		g.won = N
		return
	if g.noMoreMoves
		init_board
		return
	moved = N
	case dir
	| KEYCODE_UP
		trymove(1, 4, 1)
	| KEYCODE_LEFT
		trymove(1, 1, 4)
	| KEYCODE_DOWN
		trymove(13, -4, 1)
	| KEYCODE_RIGHT
		trymove(4, -1, 4)
	if moved
		inc g.moves
		moved = N
		addTile
	canMove
	
	
// -------------------------------------------
calc init_board
	board <=== []
	blanks = 16
	addTile
	addTile
	g.score = 0
	g.moves = 0
	g.won = N
	g.noMoreMoves = N
	log "gravar {g.best}"
//	ixdb_write(MYDB, STORE_APP, 1, adr g)	

// -------------------------------------------
calc trymove(
	p1
	ix
	iy
	)
	var
		x1
		x2		
	loop reps:4
		x1=p1
		x2=p1+ix
		p1=p1+iy	
		loop reps:3
			if board[x2] == U
				x2 = x2 + ix
			elif board[x1] == U
				board[x1] = board[x2]
				board[x2] = U
				x2 = x2 + ix
				moved = Y
			elif board[x1] == board[x2]
				board[x1] = board[x1] * 2
				g.score = g.score + board[x1]
				g.best = g.best if g.score < g.best else g.score
				if board[x1] == 2048
					g.won = Y
				inc blanks
				moved = Y
				board[x2] = U
				x1 = x1 + ix
				x2 = x2 + ix
			else 
				x1 = x1 + ix
				if board[x1] == U
					swap board[x1] <=> board[x2]
					moved = Y
				x2 = x2 + ix

// -------------------------------------------
vert slice d_game
	add 1 al d_top
	add 9 al d_board

// -------------------------------------------
horz slice d_top
	bkg = LIGHT_YELLOW
	add 50 al
		draw_rect(fill:bkg, thick:1 pt)
		draw_str("Score\n{g.score}", size:0.3, bold:Y)	
	add 50 al
		draw_rect(fill:bkg, thick:1 pt)
		draw_str("Best\n{g.best}", size:0.3, bold:Y)	
	add 50 al
		draw_rect(fill:bkg, thick:1 pt)
		draw_str("Moves\n{g.moves}", size:0.3, bold:Y)	
	
// -------------------------------------------
grid d_board
	horz slice
		skip gap al
		loop reps:4
			add 40 al
			skip gap al
	vert slice
		skip gap al
		loop reps:4
			add 40 al
			skip gap al
	under
		draw_rect(fill:#BBADA0)
	cell	
		var 
			v  = board[b.cell_seq] if is_numeric(board[b.cell_seq]) else ""
			ix = round(lg(board[b.cell_seq],base:2))
		case ix
		| >12
			ix = 13
		| >0
			inc ix
		else
			ix = 1			
		color = colors[ix]
		draw_rect(fill:color, thick:0 pt,corner:5 pt)
		draw_str(v, size:0.5, bold:Y)
	over
		if g.won or g.noMoreMoves
			var	msg : str
			if g.won
				color = DODGER_BLUE	
				msg = "Congratulations!\nYou made it.\n2048"
			if g.noMoreMoves
				color=RED	
				msg="Game over."
			draw_rect(fill:color, thick:0 pt, opacity:0.8)	
			draw_str(msg, size:0.5, bold:Y,vert:0.1,color:YELLOW)
			draw_str("Press any key.", bold:Y,vert:0.9,color:YELLOW)
		
// -------------------------------------------
track EV_TAP
	case b.cell_seq
	|2,3
		execMove(KEYCODE_UP)
	|5,9
		execMove(KEYCODE_LEFT)
	|8,12
		execMove(KEYCODE_RIGHT)
	|14,15
		execMove(KEYCODE_DOWN)
	return Y
	
// -------------------------------------------
track EV_KEYBOARD
	if e.keycode == KEYCODE_ESC
		init_board
		return Y
	execMove(e.keycode)
	return Y
	
// -------------------------------------------
calc main_init
	g.best = 0
	g.score = 0
	g.moves = 0
	ixdb_read(MYDB, STORE_APP, 1, adr g)
	init_board
	 
// -------------------------------------------
draw main_draw
	layer area:solve_rect(basis:b.box, pin:MID_CENTER, aspect:0.8) d_game		
