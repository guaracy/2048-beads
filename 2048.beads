beads 1 program Game2048

// -------------------------------------------
const
	DEBUG = N
	gap = 4
	colors = [#cdc1b4 #eee4da #ede0c8 #f2b179 #f59563 #f67c5f #f65e3b #edcf72 #edcc61 #edc850 #edc53f #edc22e #edc22e]
	
record a_hof
	date : str
	score
	
record a_game
	board : array of num
	state  : (S_PLAY, S_NOMOVES, S_WON, S_CONTINUE, S_HELP, S_MENU, S_MOBILE, S_END)
	//won : yesno
	//noMoreMoves : yesno
	//menu : yesno
	//help : yesno
	blanks : num
	moves : num
	size : num
	score : num
	best : num
	scorem : num
	hof : array of num

// -------------------------------------------
var
	g : a_game
	moved : yesno
	bkg : color
	color : color

// -------------------------------------------
calc add_tile
	var
		v = 2 if random() < 0.9 else 4
		p = random_int(1,g.blanks)
	dec g.blanks
	loop reps:16 count:ix
		if is_numeric(g.board[ix])
			continue
		dec p
		if p == 0
			g.board[ix]=v
			exit

// -------------------------------------------
calc can_move
	var
		p = 1
	//g.noMoreMoves = N
	if g.blanks > 0
		return
	loop reps:4 count:r
		loop reps:4 count:c
			log "pos: {r}x{c} p={p}" on:DEBUG
			if c < 4
				log "{g.board[p]}={g.board[p+1]}?" on:DEBUG
				if g.board[p] == g.board[p+1]
					return
			if r < 4
				log "{g.board[p]}={g.board[p+4]}?" on:DEBUG
				if g.board[p] == g.board[p+4]
					return
			inc p
	g.state = S_NOMOVES // g.noMoreMoves = Y

// -------------------------------------------
calc exec_move(
	dir
	)
	if g.state == S_WON
		g.state = S_CONTINUE
		return
	if g.state == S_NOMOVES
		init_board
		return
	moved = N
	case dir
	| KEYCODE_UP
		try_move(1, 4, 1)
	| KEYCODE_LEFT
		try_move(1, 1, 4)
	| KEYCODE_DOWN
		try_move(13, -4, 1)
	| KEYCODE_RIGHT
		try_move(4, -1, 4)
	if moved
		inc g.moves
		moved = N
		add_tile
	can_move
	
// -------------------------------------------
calc init_board
	g.board <=== []
	g.blanks = 16
	add_tile
	add_tile
	g.score = 0
	g.moves = 0
	g.state = S_PLAY
	log "gravar {g.best}"

// -------------------------------------------
calc try_move(
	p1
	ix
	iy
	)
	var
		x1
		x2		
	loop reps:4
		x1=p1
		x2=p1+ix
		p1=p1+iy	
		loop reps:3
			if g.board[x2] == U
				x2 = x2 + ix
			elif g.board[x1] == U
				g.board[x1] = g.board[x2]
				g.board[x2] = U
				x2 = x2 + ix
				moved = Y
			elif g.board[x1] == g.board[x2]
				g.board[x1] = g.board[x1] * 2
				g.score = g.score + g.board[x1]
				g.best = g.best if g.score < g.best else g.score
				if g.board[x1] == 2048
					g.state = S_WON //won = Y
				inc g.blanks
				moved = Y
				g.board[x2] = U
				x1 = x1 + ix
				x2 = x2 + ix
			else 
				x1 = x1 + ix
				if g.board[x1] == U
					swap g.board[x1] <=> g.board[x2]
					moved = Y
				x2 = x2 + ix

// -------------------------------------------
vert slice d_game
	add 1 al d_top
	add 9 al d_board

// -------------------------------------------
horz slice d_top
	bkg = LIGHT_YELLOW
	add 50 al
		draw_rect(fill:bkg, thick:1 pt)
		draw_str("Score\n{g.score}", size:0.3, bold:Y)	
	add 50 al
		draw_rect(fill:bkg, thick:1 pt)
		draw_str("Best\n{g.best}", size:0.3, bold:Y)	
	add 50 al
		draw_rect(fill:bkg, thick:1 pt)
		draw_str("Moves\n{g.moves}", size:0.3, bold:Y)	
	
// -------------------------------------------
grid d_board
	horz slice
		skip gap al
		loop reps:4
			add 40 al
			skip gap al
	vert slice
		skip gap al
		loop reps:4
			add 40 al
			skip gap al
	under
		draw_rect(fill:#BBADA0)
	cell	
		var 
			v  = g.board[b.cell_seq] if is_numeric(g.board[b.cell_seq]) else ""
			ix = round(lg(g.board[b.cell_seq],base:2))
		case ix
		| >12
			ix = 13
		| >0
			inc ix
		else
			ix = 1			
		color = colors[ix]
		var radius = min(bb.height, bb.width)*0.1
		draw_rect(fill:color, thick:0 pt,corner:radius)
		draw_str(v, size:0.5, bold:Y)
	over
		if g.state == S_WON or g.state == S_NOMOVES //g.won or g.noMoreMoves
			var	msg : str
			if g.state == S_WON //won
				color = DODGER_BLUE	
				msg = "Congratulations!\nYou made it.\n2048"
			if g.state == S_NOMOVES //g.noMoreMoves
				color=RED	
				msg="Game over."
			draw_rect(fill:color, thick:0 pt, opacity:0.8)	
			draw_str(msg, size:0.5, bold:Y,vert:0.1,color:YELLOW)
			draw_str("Press any key.", bold:Y,vert:0.9,color:YELLOW)
		
// -------------------------------------------
track EV_TAP
	case b.cell_seq
	|2,3
		exec_move(KEYCODE_UP)
	|5,9
		exec_move(KEYCODE_LEFT)
	|8,12
		exec_move(KEYCODE_RIGHT)
	|14,15
		exec_move(KEYCODE_DOWN)
	return Y
	
// -------------------------------------------
track EV_KEYBOARD
	if e.keycode == KEYCODE_ESC
		init_board
		return Y
	exec_move(e.keycode)
	return Y
	
// -------------------------------------------
calc main_init
	g.best = 0
	g.score = 0
	g.moves = 0
	init_board
	if DEBUG
		g.blanks = 0
		g.board <=== [2 4 8 4 8 2 16 4 32 128 32 8 2048 512 256 64]
		g.board <=== [2 4 8 4 8 2 16 4 32 128 32 8 1024 1024 256 64]
		can_move
	 
// -------------------------------------------
draw main_draw
	layer area:solve_rect(basis:b.box, pin:MID_CENTER, aspect:0.8) d_game		
